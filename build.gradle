import com.diffplug.gradle.spotless.SpotlessExtension

// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    alias(libs.plugins.android.application) apply false
    alias(libs.plugins.jetbrains.kotlin.android) apply false
    alias(libs.plugins.spotless) apply false
}

subprojects {
    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target("**/*.kt")
            targetExclude("**/build/**/*.kt")
            ktlint()
        }
    }
}

tasks.register("formatTestAssemble") {
    dependsOn(
            subprojects.collect { subproject ->
                [
                        subproject.tasks.named("verifyPaparazziDebug"),
                        subproject.tasks.named("spotlessApply"),
                        subproject.tasks.named("spotlessCheck"),
                        subproject.tasks.named("testDebugUnitTest"),
                        subproject.tasks.named("connectedDebugAndroidTest"),
                        subproject.tasks.named("assembleDebug")
                ]
            }.flatten())
    group = "Build"
    description = "Runs Paparazzi, Spotless, unit tests, instrumentation tests, and assembles the debug build."
}